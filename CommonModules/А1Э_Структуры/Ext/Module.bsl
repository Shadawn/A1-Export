// Возвращает значение структуры по ключу. Если значение отсутствует ИЛИ передана не структура, то возвращает ЗначениеПоУмолчанию.
//
// Параметры:
//  Структура			 - Произвольный - 
//  ИмяСвойства			 - Строка - 
//  ЗначениеПоУмолчанию	 - Произвольный	 - 
// 
// Возвращаемое значение:
//   - 
//
Функция ЗначениеСвойства(Структура, ИмяСвойства, ЗначениеПоУмолчанию = Неопределено) Экспорт
	#Если Сервер И НЕ Сервер Тогда
		Структура = Новый Структура;		
	#КонецЕсли 
	Перем ЗначениеСвойства;
	Если НЕ ЯвляетсяСтруктурой(Структура) Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	ЕстьСвойство = Структура.Свойство(ИмяСвойства, ЗначениеСвойства);
	Если ЕстьСвойство Тогда	
		Возврат ЗначениеСвойства;
	Иначе
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
КонецФункции 

// Определяет наличие свойства у переданной структуры. Если свойство присутствует, оно записывается в переданную переменную Значение.
// Не структуры рассматриваются как не имеющие свойств (и всегда возвращают Ложь).
//
// Параметры:
//  Структура	 - Произвольный	 - 
//  ИмяСвойства	 - Строка - 
//  Значение	 - Произвольный	 - 
// 
// Возвращаемое значение:
//   - Булево
//
Функция Свойство(Структура, ИмяСвойства, Значение = Неопределено) Экспорт 
	ЗначениеСвойства = ЗначениеСвойства(Структура, ИмяСвойства, Null);
	Если ЗначениеСвойства <> Null Тогда
		Значение = ЗначениеСвойства;
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

// Возвращает Структуру, соответствующую переданному значению по следующей логике
//   Структура - возвращает её саму.
//   ФиксированнаяСтруктура - возвращает соответствующую Структуру.
//   Неопределено - возвращает Новый Структура;
//
// Параметры:
//  Данные	 - Структура,ФиксированнаяСтруктура,Неопределено - 
// 
// Возвращаемое значение:
//   - Структура
//
Функция Структура(Данные) Экспорт
	Если Данные = Неопределено Тогда
		Возврат Новый Структура;
	ИначеЕсли ТипЗнч(Данные) = Тип("Структура") Тогда
		Возврат Данные;
	ИначеЕсли ТипЗнч(Данные) = Тип("ФиксированнаяСтруктура") Тогда
		Возврат Новый Структура(Данные);
	Иначе
		А1Э_Служебный.ИсключениеНеверныйТип("Данные", ИмяМодуля() + ".Структура", Данные, "Структура,ФиксированнаяСтруктура,Неопределено");
	КонецЕсли;
КонецФункции

// Возвращает Соответствие, соответствующую переданному значению по следующей логике
//   Соответствие - возвращает её саму.
//   ФиксированноеСоответствие - возвращает Соответствие.
//   Неопределено - возвращает Новый Соответствие;
//
// Параметры:
//  Данные	 - Соответствие,ФиксированноеСоответствие,Неопределено - 
// 
// Возвращаемое значение:
//   - Соответствие
//
Функция Соответствие(Данные) Экспорт 
	Если Данные = Неопределено Тогда
		Возврат Новый Соответствие;
	ИначеЕсли ТипЗнч(Данные) = Тип("Соответствие") Тогда
		Возврат Данные;
	ИначеЕсли ТипЗнч(Данные) = Тип("ФиксированноеСоответствие") Тогда
		Возврат Новый Соответствие(Данные);
	Иначе
		А1Э_Служебный.ИсключениеНеверныйТип("Данные", ИмяМодуля() + ".Соответствие", Данные, "Соответствие,ФиксированноеСоответствие,Неопределено");
	КонецЕсли;
КонецФункции

// Копирует свойства из одной структуры в другую. Рассматривает Неопределено как структуру без свойств.
//
// Параметры:
//  Приемник - Структура - 
//  Источник - Структура,Неопределено - 
//  Замещать - Булево - 
//  КопироватьНезаполненные - Булево - 
// 
// Возвращаемое значение:
//   - 
//
Функция СкопироватьСвойства(Приемник, Источник, Замещать = Ложь, КопироватьНезаполненные = Истина) Экспорт
	
	Если Источник = Неопределено Тогда Возврат Приемник; КонецЕсли;
	
	Если ТипЗнч(Приемник) <> Тип("Структура") Тогда
		Приемник = Структура(Приемник); //Исправляет ошибку с немутируемыми структурами (ПараметрыЗапроса, ect).
	КонецЕсли;
	
	Для Каждого Пара Из Источник Цикл
		Если Приемник.Свойство(Пара.Ключ) И Замещать <> Истина Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ А1Э_СтандартныеТипы.Заполнено(Пара.Значение) И НЕ КопироватьНезаполненные Тогда
			Продолжить;
		КонецЕсли;
		Приемник.Вставить(Пара.Ключ, Пара.Значение);
	КонецЦикла;
	
	Возврат Приемник;
КонецФункции

// Создает структуру. Предназначена для написания конструктора, где каждое свойство и значение на новой строке, что улучшает читаемость кода.
//
// Параметры:
//  К<Х>	 - Строка - ключи структуры.
//  З<Х>	 - Произвольный	 - значения структуры. 
// 
// Возвращаемое значение:
//   - Структура
//
Функция Создать(К1 = Null, З1 = Null, К2 = Null, З2 = Null, К3 = Null, З3 = Null, К4 = Null, З4 = Null, К5 = Null, З5 = Null, К6 = Null, З6 = Null, К7 = Null, З7 = Null, К8 = Null, З8 = Null, К9 = Null, З9 = Null, К10 = Null, З10 = Null, К11 = Null, З11 = Null, К12 = Null, З12 = Null, К13 = Null, З13 = Null, К14 = Null, З14 = Null, К15 = Null, З15 = Null) Экспорт
	Структура = Новый Структура;
	Возврат ДобавитьКлючиИЗначения(Структура, К1, З1, К2, З2, К3, З3, К4, З4, К5, З5, К6, З6, К7, З7, К8, З8, К9, З9, К10, З10, К11, З11, К12, З12, К13, З13, К14, З14, К15, З15); 
КонецФункции

// Создает фиксированную структуру. Предназначена для написания конструктора, где каждое свойство и значение на новой строке, что улучшает читаемость кода.
//
// Параметры:
//  К<Х>	 - Строка - ключи структуры.
//  З<Х>	 - Произвольный	 - значения структуры. 
// 
// Возвращаемое значение:
//   - ФиксированнаяСтруктура
//  
Функция СоздатьФиксированную(К1 = Null, З1 = Null, К2 = Null, З2 = Null, К3 = Null, З3 = Null, К4 = Null, З4 = Null, К5 = Null, З5 = Null, К6 = Null, З6 = Null, К7 = Null, З7 = Null, К8 = Null, З8 = Null, К9 = Null, З9 = Null, К10 = Null, З10 = Null, К11 = Null, З11 = Null, К12 = Null, З12 = Null, К13 = Null, З13 = Null, К14 = Null, З14 = Null, К15 = Null, З15 = Null) Экспорт
	Возврат Новый ФиксированнаяСтруктура(Создать(К1, З1, К2, З2, К3, З3, К4, З4, К5, З5, К6, З6, К7, З7, К8, З8, К9, З9, К10, З10, К11, З11, К12, З12, К13, З13, К14, З14, К15, З15));
КонецФункции

// Возвращает зафиксированную версию переданного значения. Если значение уже зафиксировано, возвращает само значение.
// НЕ ВЫПОЛНЯЕТ рекурсивного обхода вложенных элементов.
//
// Параметры:
//  Значение - Структура,Соответствие,ФиксированнаяСтруктура,ФиксированноеСоответствие - 
// 
// Возвращаемое значение:
//   - ФиксированнаяСтруктура,ФиксированноеСоответствие 
//
Функция Зафиксировать(Значение) Экспорт
	Если ТипЗнч(Значение) = Тип("Структура") Тогда
		Возврат Новый ФиксированнаяСтруктура(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("Соответствие") Тогда
		Возврат Новый ФиксированноеСоответствие(Значение);
	ИначеЕсли Зафиксировано(Значение) Тогда
		Возврат Значение; 
	Иначе
		А1Э_Служебный.ИсключениеНеверныйТип("Значение", ИмяМодуля() + ".Зафиксировать", Значение, "Структура,ФиксированнаяСтруктура,Соответствие,ФиксированноеСоответствие");
	КонецЕсли;
	
КонецФункции

// Возвращает не зафиксированную версию переданного значения. Если значение не зафиксировано, возвращает само значение.
// НЕ ВЫПОЛНЯЕТ рекурсивного обхода вложенных элементов.
//
// Параметры:
//  Значение - Структура,Соответствие,ФиксированнаяСтруктура,ФиксированноеСоответствие - 
// 
// Возвращаемое значение:
//   - Структура,Соответствие 
//
Функция Дефиксировать(Значение) Экспорт
	ТипЗнч = ТипЗнч(Значение);
	Если ТипЗнч = Тип("ФиксированнаяСтруктура") Тогда
		Возврат Новый Структура(Значение);
	ИначеЕсли ТипЗнч = Тип("ФиксированноеСоответствие") Тогда
		Возврат Новый Соответствие(Значение);
	ИначеЕсли ТипЗнч = Тип("Структура") Или ТипЗнч = Тип("Соответствие") Тогда
		Возврат Значение;
	Иначе
		А1Э_Служебный.ИсключениеНеверныйТип("Значение", ИмяМодуля() + ".Дефиксировать", Значение, "Структура,ФиксированнаяСтруктура,Соответствие,ФиксированноеСоответствие")
	КонецЕсли;
КонецФункции

// Создает соответствие. Предназначена для написания конструктора, где каждое свойство и значение на новой строке, что улучшает читаемость кода.
//
// Параметры:
//  К<Х>	 - Примитивный - ключи соответствия.
//  З<Х>	 - Произвольный	 - значения структуры. 
// 
// Возвращаемое значение:
//   - Соответствие
//
Функция СоздатьСоответствие(К1 = Null, З1 = Null, К2 = Null, З2 = Null, К3 = Null, З3 = Null, К4 = Null, З4 = Null, К5 = Null, З5 = Null, К6 = Null, З6 = Null, К7 = Null, З7 = Null, К8 = Null, З8 = Null, К9 = Null, З9 = Null, К10 = Null, З10 = Null, К11 = Null, З11 = Null, К12 = Null, З12 = Null, К13 = Null, З13 = Null, К14 = Null, З14 = Null, К15 = Null, З15 = Null) Экспорт
	Соответствие = Новый Соответствие;
	Возврат ДобавитьКлючиИЗначения(Соответствие, К1, З1, К2, З2, К3, З3, К4, З4, К5, З5, К6, З6, К7, З7, К8, З8, К9, З9, К10, З10, К11, З11, К12, З12, К13, З13, К14, З14, К15, З15);	
КонецФункции

// Добавляет ключи и значения в структуру/соответствие. Возвращает переданную структуру/соответствие
//
// Параметры:
//  Структура	 - Структура,Соответствие - 
//  К<Х>		 - Строка,Примитивный - 
//  З<Х>		 - Произвольный	 - 
// 
// Возвращаемое значение:
//   - Структура,Соответствие 
//
Функция ДобавитьКлючиИЗначения(Структура, К1 = Null, З1 = Null, К2 = Null, З2 = Null, К3 = Null, З3 = Null, К4 = Null, З4 = Null, К5 = Null, З5 = Null, К6 = Null, З6 = Null, К7 = Null, З7 = Null, К8 = Null, З8 = Null, К9 = Null, З9 = Null, К10 = Null, З10 = Null, К11 = Null, З11 = Null, К12 = Null, З12 = Null, К13 = Null, З13 = Null, К14 = Null, З14 = Null, К15 = Null, З15 = Null) Экспорт
	Если К1 = Null Тогда Возврат Структура; КонецЕсли;
	Структура.Вставить(К1, З1);
	Если К2 = Null Тогда Возврат Структура; КонецЕсли;
	Структура.Вставить(К2, З2);
	Если К3 = Null Тогда Возврат Структура; КонецЕсли;
	Структура.Вставить(К3, З3);
	Если К4 = Null Тогда Возврат Структура; КонецЕсли;
	Структура.Вставить(К4, З4);
	Если К5 = Null Тогда Возврат Структура; КонецЕсли;
	Структура.Вставить(К5, З5);
	Если К6 = Null Тогда Возврат Структура; КонецЕсли;
	Структура.Вставить(К6, З6);
	Если К7 = Null Тогда Возврат Структура; КонецЕсли;
	Структура.Вставить(К7, З7);
	Если К8 = Null Тогда Возврат Структура; КонецЕсли;
	Структура.Вставить(К8, З8);
	Если К9 = Null Тогда Возврат Структура; КонецЕсли;
	Структура.Вставить(К9, З9);
	Если К10 = Null Тогда Возврат Структура; КонецЕсли;
	Структура.Вставить(К10, З10);
	Если К11 = Null Тогда Возврат Структура; КонецЕсли;
	Структура.Вставить(К11, З11);
	Если К12 = Null Тогда Возврат Структура; КонецЕсли;
	Структура.Вставить(К12, З12);
	Если К13 = Null Тогда Возврат Структура; КонецЕсли;
	Структура.Вставить(К13, З13);
	Если К14 = Null Тогда Возврат Структура; КонецЕсли;
	Структура.Вставить(К14, З14);
	Если К15 = Null Тогда Возврат Структура; КонецЕсли;
	Структура.Вставить(К15, З15);
	Возврат Структура;
КонецФункции

// Возвращает копию структуры. Копирование происходит только по верхнему уровню,
// обход вложенных элементов и глубокое копирование НЕ ВЫПОЛНЯЕТСЯ.
//
// Параметры:
//  Структура	 - Структура - 
// 
// Возвращаемое значение:
//   - Структура
//
Функция Скопировать(Структура) Экспорт
	Если Структура = Неопределено Тогда Возврат Новый Структура; КонецЕсли;
	НоваяСтруктура = Новый Структура;
	Для Каждого Пара Из Структура Цикл
		НоваяСтруктура.Вставить(Пара.Ключ, Пара.Значение);	
	КонецЦикла;
	Возврат НоваяСтруктура;
КонецФункции

// Возвращает новую структуру, представляющую копию шаблона с добавленным перечнем ключей.
// При совпадении ключей шаблона и добавляемых ключей замены НЕ ПРОИСХОДИТ.
//
// Параметры:
//  Шаблон	- Структура	 - 
//  К<Х>	- Строка - 
//  З<Х>	- Произвольный - 
// 
// Возвращаемое значение:
//   - Структура
//
Функция СкопироватьВШаблон(Шаблон, К1 = Null, З1 = Null, К2 = Null, З2 = Null, К3 = Null, З3 = Null, К4 = Null, З4 = Null, К5 = Null, З5 = Null, К6 = Null, З6 = Null, К7 = Null, З7 = Null, К8 = Null, З8 = Null, К9 = Null, З9 = Null, К10 = Null, З10 = Null, К11 = Null, З11 = Null, К12 = Null, З12 = Null, К13 = Null, З13 = Null, К14 = Null, З14 = Null, К15 = Null, З15 = Null) Экспорт
	Результат = Создать(К1, З1, К2, З2, К3, З3, К4, З4, К5, З5, К6, З6, К7, З7, К8, З8, К9, З9, К10, З10, К11, З11, К12, З12, К13, З13, К14, З14, К15, З15);
	Возврат СкопироватьСвойства(Результат, Шаблон);
КонецФункции

// Возвращает новую структуру, представляющую копию шаблона с добавленным перечнем ключей.
// При совпадении ключей шаблона и добавляемых ключей ПРОИСХОДИТ ЗАМЕНА значений в копии шаблона.
//
// Параметры:
//  Шаблон	- Структура	 - 
//  К<Х>	- Строка - 
//  З<Х>	- Произвольный - 
// 
// Возвращаемое значение:
//   - Структура 
//
Функция СкопироватьВШаблонСЗаменой(Структура, К1 = Null, З1 = Null, К2 = Null, З2 = Null, К3 = Null, З3 = Null, К4 = Null, З4 = Null, К5 = Null, З5 = Null, К6 = Null, З6 = Null, К7 = Null, З7 = Null, К8 = Null, З8 = Null, К9 = Null, З9 = Null, К10 = Null, З10 = Null, К11 = Null, З11 = Null, К12 = Null, З12 = Null, К13 = Null, З13 = Null, К14 = Null, З14 = Null, К15 = Null, З15 = Null) Экспорт
	Результат = Создать(К1, З1, К2, З2, К3, З3, К4, З4, К5, З5, К6, З6, К7, З7, К8, З8, К9, З9, К10, З10, К11, З11, К12, З12, К13, З13, К14, З14, К15, З15);
	Возврат СкопироватьСвойства(Результат, Структура, Истина);
КонецФункции 

// Добавляет ключи (без значений) к переданной структуре.
//
// Параметры:
//  Структура	 - Структура - 
//  Ключи		 - Строка,Массив<Строка> - допустима передача нескольких ключей одной строкой через запятую. 
// 
// Возвращаемое значение:
//   - 
//
Функция ДобавитьКлючи(Структура, Знач Ключи) Экспорт
	Ключи = А1Э_Массивы.Массив(Ключи);
	Для Каждого Ключ Из Ключи Цикл
		Структура.Вставить(Ключ);
	КонецЦикла;
КонецФункции

// Удаляет ключи у переданной структуры
//
// Параметры:
//  Структура	 - Структура - 
//  Ключи		 - Строка,Массив<Строка> - допустима передача нескольких ключей одной строкой через запятую.
// 
// Возвращаемое значение:
//   - 
//
Функция УдалитьКлючи(Структура, Знач Ключи) Экспорт
	Ключи = А1Э_Массивы.Массив(Ключи);
	Для Каждого Ключ Из Ключи Цикл
		Структура.Удалить(Ключ);
	КонецЦикла;
КонецФункции

Функция Класс(Структура) Экспорт
	Возврат А1Э_Классы.Класс(Структура);	
КонецФункции

// Добавляет ключ/значение к структуре, если оно отсутствует.
// Если ключ уже присутствует в структуре, то изменений не происходит.
//
// Параметры:
//  Структура	 - Структура -
//  Ключ		 - Строка - 
//  Значение	 - Произвольный	 - 
// 
// Возвращаемое значение:
//   - 
//
Функция ВставитьЕслиОтсутствует(Структура, Ключ, Значение) Экспорт
	Если НЕ Структура.Свойство(Ключ) Тогда
		Структура.Вставить(Ключ, Значение);
	КонецЕсли;	
КонецФункции 

// Определяет, является ли переданное значение структурой (или фиксированной структурой)
//
// Параметры:
//  Значение - Произвольный	 - 
// 
// Возвращаемое значение:
//   - Булево
//
Функция ЯвляетсяСтруктурой(Значение) Экспорт
	Возврат ТипЗнч(Значение) = Тип("Структура") Или ТипЗнч(Значение) = Тип("ФиксированнаяСтруктура");
КонецФункции

// Определяет, является ли переданное значение соответствием (или фиксированным соответствием)
//
// Параметры:
//  Значение - Произвольный	 - 
// 
// Возвращаемое значение:
//   - Булево 
//
Функция ЯвляетсяСоответствием(Значение) Экспорт
	Возврат ТипЗнч(Значение) = Тип("Соответствие") Или ТипЗнч(Значение) = Тип("ФиксированноеСоответствие");
КонецФункции 

// Определяет, является ли переданное значение фиксированным.
//
// Параметры:
//  Значение - Произвольный	 - 
// 
// Возвращаемое значение:
//   - Булево 
//
Функция Зафиксировано(Значение) Экспорт 
	ТипЗнч = ТипЗнч(Значение);
	Возврат ТипЗнч = Тип("ФиксированнаяСтруктура") Или ТипЗнч = Тип("ФиксированноеСоответствие");
КонецФункции

#Область Устарело

Функция СкопироватьССозданием(Структура) Экспорт
	Возврат Скопировать(Структура);		
КонецФункции

#КонецОбласти

Функция ИмяМодуля() Экспорт
	Возврат "А1Э_Структуры";
КонецФункции 