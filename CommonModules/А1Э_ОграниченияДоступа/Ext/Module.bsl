#Область ПроверкаМеханизмаОграниченийДоступа

Функция ПроверитьМеханизмОграниченийДоступа(Контекст) Экспорт 
	Если Метаданные.РегистрыСведений.Найти("А1_РазрешенныеОбъекты") = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Перечисление = Метаданные.Перечисления.Найти("А1_ОбъектыДоступа");
	Если Перечисление = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Каждого ОбъектДоступа Из Перечисление.ЗначенияПеречисления Цикл
		ПараметрСеанса = ПараметрСеансаРазрешенияДоступа(ОбъектДоступа.Имя);
		Если ПараметрСеанса <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		А1Э_Сообщения.СообщитьПоШаблону(
		"Для объекта доступа [ОбъектДоступа] не найден параметр сеанса, хранящий разрешения доступа." + Символы.ПС +
		"Параметр сеанса должен называться [ИмяПараметраСеанса]",
		Новый Структура("ОбъектДоступа,ИмяПараметраСеанса", ОбъектДоступа.Имя, ИмяПараметраСеансаРазрешенияДоступа(ОбъектДоступа.Имя)));
	КонецЦикла;
	
КонецФункции

#КонецОбласти 

Функция ЗаполнитьПараметрыСеанса(Контекст = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Если Метаданные.РегистрыСведений.Найти("А1_ОграниченияДоступа") = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	РазрешенныеОбъекты.ОбъектДоступа КАК ОбъектДоступа,
	|	РазрешенныеОбъекты.ЗначениеДоступа КАК ЗначениеДоступа
	|ИЗ
	|	РегистрСведений.А1_ОграниченияДоступа КАК РазрешенныеОбъекты
	|ГДЕ
	|	РазрешенныеОбъекты.Пользователь = &Пользователь
	|ИТОГИ ПО
	|	ОбъектДоступа");
	МодульПользователи = Вычислить("ПользователиКлиентСервер");
	Запрос.УстановитьПараметр("Пользователь", МодульПользователи.ТекущийПользователь());
	ВыборкаОбъектДоступа = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОбъектДоступа.Следующий() Цикл
		ПараметрСеанса = ПараметрСеансаРазрешенияДоступа(ВыборкаОбъектДоступа.ОбъектДоступа);
		Если ПараметрСеанса = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		МассивЗначенийДоступа = Новый Массив;
		Выборка = ВыборкаОбъектДоступа.Выбрать();
		Пока Выборка.Следующий() Цикл
			МассивЗначенийДоступа.Добавить(Выборка.ЗначениеДоступа);
		КонецЦикла;
		ПараметрыСеанса[ПараметрСеанса.Имя] = Новый ФиксированныйМассив(МассивЗначенийДоступа);
	КонецЦикла;
	
КонецФункции

Функция ПараметрСеансаРазрешенияДоступа(ОбъектДоступа) Экспорт 
	ИмяПараметраСеанса = ИмяПараметраСеансаРазрешенияДоступа(ОбъектДоступа);
	Возврат Метаданные.ПараметрыСеанса.Найти(ИмяПараметраСеанса);
КонецФункции 

Функция ИмяПараметраСеансаРазрешенияДоступа(ОбъектДоступа) Экспорт
	ИмяТипа = "ПеречислениеСсылка.А1_ОбъектыДоступа";
	Если ТипЗнч(ОбъектДоступа) = Тип(ИмяТипа) Тогда
		ИмяОбъектаДоступа = А1Э_Перечисления.ИмяЗначения(ОбъектДоступа);
	ИначеЕсли ТипЗнч(ОбъектДоступа) = Тип("Строка") Тогда
		ИмяОбъектаДоступа = ОбъектДоступа;
	Иначе
		А1Э_Служебный.ИсключениеНеверныйТип("ОбъектДоступа", "А1Э_ОграниченияДоступа.ИмяПараметраСеансаРазрешенияДоступа", ОбъектДоступа, "ПеречислениеСсылка.А1_ОбъектыДоступа,Строка");
	КонецЕсли;
	
	Возврат "А1_РазрешенияДоступа_" + ИмяОбъектаДоступа;	
КонецФункции 

